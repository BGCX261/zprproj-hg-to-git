<?xml version="1.0" encoding="utf-8"?>
<!--  authors: Rafal Witowski, Bartosz Stefanski -->

<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" minWidth="955" minHeight="600"
			   width="400" height="124"
			   title="solving"
			   creationComplete="init()">
    <fx:Script>
        <![CDATA[
                
            import config.Config;
                       
            import mx.managers.PopUpManager;
            import mx.rpc.events.ResultEvent;
            import mx.rpc.remoting.mxml.RemoteObject;
            
            import mx.controls.Alert;                        
            
            private var remoteObj : RemoteObject = null;
            private var timer : Timer;
            private var tspId_ : int = 0;
            private var timer_finalize : Boolean = false;
            private var i : int = 0;
            
            private function init() : void 
            {
                remoteObj = new RemoteObject();
                remoteObj.endpoint = Config.getInstance().getEndpoint();
                remoteObj.destination = Config.getInstance().getDestination();
                remoteObj.tspState.addEventListener("result", onTspState);
                remoteObj.tspResult.addEventListener("result", onTspResult);                                
            }
            
            //start to watch the command
            public function startWatch( tsp_id : int ) : void 
            {
                this.tspId_ = tsp_id;
                remoteObj.tspSolve(tspId_);                               
                                                
                timer_finalize = false;
                timer = new Timer(1000); /* 1000ms == 1second */
                timer.addEventListener(TimerEvent.TIMER, onTimer);                                                                
                timer.start();
            }
            
            private function onTimer(evt:TimerEvent) : void 
            {
                if(timer_finalize) 
                {
                    timer.stop();
                }
                else 
                {
                    progress.setProgress(i, 100);
                    i += 10;                                                                        
                    i %= 100;
                                                            
                    remoteObj.tspState(tspId_);
                }
            }
            
            private function onTspState(re:ResultEvent) : void 
            {
                
                var ret_data : String = re.result as String;                                  
                progress.label = ret_data;                            

                if( ret_data == "SOLVED" )
                {
                    timer_finalize = true;                     
                    remoteObj.tspResult(this.tspId_);
                }                 
            }
            
            private function onTspResult(re:ResultEvent) : void
            {
                dispatchEvent(new Event(Event.CHANGE));
                PopUpManager.removePopUp(this);
            }                        

        ]]>
    </fx:Script>

    <s:VGroup left="5" right="5" top="10" bottom="5" horizontalAlign="center">
		<mx:ProgressBar id="progress" width = "100%" height="100%"
						label="starting"
						mode="manual" minimum="0" maximum="100" labelPlacement="center"  />
	</s:VGroup>	      
        
</s:TitleWindow>
